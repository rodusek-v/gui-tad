World:
    places += Place
    objects = ObjectDefs
    connections = Connections
    commands = CommandDefs
    flags = Flags
    player = Player
    finish = Finish
;

Container:
    Place | Player | Object
;

Place:
    'place' name = WID '{'
        description = DescriptionSection
        contains = ObjectsSection
        (blockade = Blockade)?
    '}'
;

ObjectDefs:
    'object_defs {'
        objects *= Object
    '}' 
;

CommandDefs:
    'command_defs {'
        commands *= Command
    '}'
;

Object:
    'object' name = WID '{'
        description = DescriptionSection
        (contains = ObjectsSection)?
        'pickable:' pickable = BOOL
        ('container:' container = [Container])?
    '}'
;

ObjectsSection:
    'objects {'
        objects *= [Object]
    '}'
;

Flags:
    'flags {'
        flags += Flag
    '}'
;

Flag:
    'flag' name = WID '{'
        'activated:' activated = BOOL
        'action_on_true:' action_true = Action
        'action_on_false:' action_false = Action
    '}'
;

Action:
    '{'
        ('message:' message = STRING)?
        ('dependencies:' '[' dependencies += Condition [','] ']')?
    '}'
;

Condition:
    flag = [Flag] '==' value = BOOL
;

Command:
    'command' text += CommandText [','] '{'
        operation = Operation
    '}' 
;

CommandText:
    '"' predicate = WID (object = WID)? '"'
;

Operation:
    MessageOperation | CRUDOperation | FlagOperation
;

MessageOperation:
    'message:' message = STRING
;

CRUDOperation:
    (require_prop = RequireProp)+
    crud_props += CRUDProp
    'success:' success = STRING
    'fail:' fail = STRING
    (located_prop = LocatedProp)?
    (flag_prop = FlagProp)?
;

FlagOperation:
    flag_prop = FlagProp
    'success:' success = STRING
    'fail:' fail = STRING
    (require_prop = RequireProp)*
    (located_prop = LocatedProp)?
;

FlagProp:
    'flag:' flag = [Flag] '==' value = BOOL
;

RequireProp:
    RequirePlaceProp | RequireInventoryProp
;

RequirePlaceProp:
    'is_present:' '[' require += [Object] ']'
;

RequireInventoryProp:
    'is_carried:' '[' require += [Object] ']'
;

LocatedProp:
    'at:' located = [Place]
;

CRUDProp:
    type = CRUD ':' item = [Object] 
    (', destination:' destination = [Container])? 
;

CRUD:
    'create' | 'delete' | 'move'
;

DescriptionSection:
    '"""'
        'name:' name = /.*/
        'description:' description = /.*/
    '"""'
;

Blockade:
    'blockade {'
        blocks += Block
    '}'
;

Block:
    'flag' '=' flag = [Flag] ',' 'direction' '=' direction = Direction (',' 'allowed_turns' '=' turns = Turn)? 
;

Turn:
    value = INT
;

Connections:
    'connections {'
        connections *= Connection
    '}'
;

Connection:
    from_ = [Place] ',' direction = Direction ',' to_ = [Place] 
;

Player:
    'player {'
        'name:' name = WID
        'position:' position = [Place]
        'inventory {'
            items *= [Object]
        '}'
    '}'
;

Finish:
    'finish {'
        'position:' position = [Place]
        (flag_prop = FlagProp)?
    '}'
;

Direction:
    'N' | 'S' | 'E' | 'W'
;

Keyword:
    'place' | 'items' | 'item' | 'name' | 
    'description' | 'pickable' | 'flags' | 'flag' |
    'activated' | 'false_mgs' | 'true_msg' | 'require' |
    'dependencies' | 'blockade' | 'connections' |
    'player' | 'start' | 'inventory' | 'finish'
;

WID: 
    !Keyword ID
;