places {
    place Room1 {
        """
            name: Room1
            description: You are in room One
        """

        contains {
            apple
        }
    }
    place Room2 {
        """
            name: Room2
            description: You are in room Two, entrance to the room Four is blocked by door  
        """

        contains {
            gun
            ladder
        }

        blockade {
            flag = is_door_opened, direction = N
        }
    }
    place Room3 {
        """
            name: Room3
            description: You are in room Three, there is an attic entrance
        """
        
        contains {
            chest
        }
    }
    place Room4 {
        """
            name: Room4
            description: You are in room Four
        """

        contains {}
    }
    place Room5 {
        """
            name: Room5
            description: You are in room Five
        """

        contains {
            monster
        }

        blockade {
            flag = is_monster_defeated, direction = S, allowed_turns = 1
        }
    }
    place Room6 {
        """
            name: Room6
            description: You are in room Six, there is a some kind of lever
        """

        contains {}
    }
    place Room7 {
        """
            name: Room7
            description: You are in room Seven, there is picture hanging on the wall
        """

        contains {}
    }
}

objects {
    object apple {
        """
            name: an apple
            description: Big green apple
        """
        contains {}
        pickable: true
        container: Room1
    }
    object chest {
        """
            name: small chest
            description: Small chest for jewelery
        """
        contains {
            key
        }
        pickable: false
        container: Room3
    }
    object key {
        """
            name: gold key
            description: Golden key that has cross on it
        """
        contains {}
        pickable: true
        container: chest
    }
    object seeds {
        """
            name: apple seeds
            description: Seeds from fresh apple
        """
        contains {}
        pickable: true
    }
    object monster {
        """
            name: evil monster
            description: Evil monster that you need to defeat
        """
        contains {}
        pickable: false
        container: Room5
    }
    object gun {
        """
            name: gun
            description: Powerful gun
        """
        contains {}
        pickable: true
        container: Room2
    }
    object ladder {
        """
            name: iron ladder
            description: Very tall ladder
        """
        contains {}
        pickable: true
        container: Room2
    }
}

connections {
    Room1, N, Room2;
    Room1, E, Room5;
    Room2, W, Room3;
    Room2, N, Room4;
    Room5, S, Room6;
}

commands {
    command "examine chest" {
        is_present: [chest]
        move: key
        success: "You have found golden key."
        fail: ""
        flag: examined_chest == false
    }
    command "examine" {
        message: "Nothing catches your eye."
    }
    command "examine gun" {
        message: ""
        item: gun
    }
    command "open door" {
        flag: is_door_opened == false
        success: ""
        fail: "It's locked."
        at: Room2
    }
    command "close door" {
        flag: is_door_opened == true
        success: ""
        fail: "Door is already closed."
        at: Room2
    }
    command "unlock door" {
        flag: is_door_unlocked == false
        success: "You have unlocked the door."
        fail: "You don't have key to unlock it."
        is_carried: [key]
        at: Room2
    }
    command "lock door" {
        flag: is_door_unlocked == true
        success: "You have locked the door."
        fail: "I can't do it."
        is_carried: [key]
        at: Room2
    }
    command "extract seeds", "derivate seeds", "break apple" {
        is_carried: [apple]
        create: seeds
        delete: apple
        success: "You have extract seeds from apple."
        fail: "From what?"
    }
    command "shot monster" {
        is_carried: [gun]
        is_present: [monster]
        delete: monster
        success: "You have defeated monster."
        fail: "Monster killed you."
        flag: is_monster_defeated == false
    }
    command "pull lever" {
        flag: is_lever_pulled == false
        success: "You pulled the lever. Something happend."
        fail: ""
        at: Room6
    }
    command "climb up" {
        from: Room3
        to: Room7
        success: "You have climbed into old and dark attic."
        fail: "With what?"
        can_die: false
        is_present: [ladder]
    }
    command "get down" {
        from: Room7
        to: Room3
        success: ""
        fail: ""
        can_die: false
    }
    command "examine picture" {
        message: "You can see a monster that is shot by a man with a hat."
        at: Room7
    }
}

flags {
    flag is_door_unlocked {
        activated: false
        action_on_true: {
            message: "Door is already unlocked."
        }
        action_on_false: {
            message: "Door is already locked."
            dependencies: [is_door_unlocked == true, is_door_opened == false]
        }
    }
    flag is_door_opened {
        activated: false
        action_on_true: {
            message: "Door is already open."
            dependencies: [is_door_unlocked == true]
        }
        action_on_false: {
            message: "Door is already closed."
        }
    }
    flag examined_chest {
        activated: false
        action_on_true: {
            message: "Nothing cathes your eye."
        }
        action_on_false: {}
    }
    flag is_monster_defeated {
        activated: false
        action_on_true: {
            message: "You have already defeat the monster."
        }
        action_on_false: {
            message: "Monster got really close to you and it has killed you."
        }
    }
    flag is_lever_pulled {
        activated: false
        action_on_true: {
            message: "You have already pull the lever."
        }
        action_on_false: {}
    }
}

player {
    name: Player1
    position: Room1
    inventory {}
}

finish {
    position: Room4
    flag: is_lever_pulled == true
}