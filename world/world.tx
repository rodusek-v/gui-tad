World:
    places += Place
    objects = ObjectDefs
    connections = Connections
    commands = CommandDefs
    flags = Flags
    player = Player
;

Place:
    'place' name = WID '{'
        description = DescriptionSection
        contains = ObjectsSection
        (blockade = Blockade)?
    '}'
;

ObjectDefs:
    'object_defs {'
        objects *= Object
    '}' 
;

CommandDefs:
    'command_defs {'
        commands *= Command
    '}'
;

Object:
    'object' name = WID '{'
        description = DescriptionSection
        (contains = ObjectsSection)?
        'pickable:' pickable = BOOL
    '}'
;

ObjectsSection:
    'objects {'
        objects *= [Object]
    '}'
;

Flags:
    'flags {'
        flags += Flag
    '}'
;

Flag:
    'flag' name = WID '{'
        'activated:' activated = BOOL
        (true_prop = TrueMsgProp)?
        (false_prop = FalseMsgProp)?
        (depend_on_prop = DependOnProp)?
    '}'
;

TrueMsgProp:
    'true_msg:' message = STRING
;

FalseMsgProp:
    'false_msg:' message = STRING
;

DependOnProp:
    'dependencies:' dependencies += [Flag]
;

Command:
    'command' names += WID '{'
        operations += Operation
    '}' 
;

Operation:
    'on' (object = WID)? '{'
        'message:' message = STRING
        (flag_prop = FlagProp)?
        (require_prop = RequireProp)*
        (located_prop = LocatedProp)?
        (crud_prop = CRUDProp)?
    '}'
;

FlagProp:
    'flag:' flag = [Flag]
;

RequireProp:
    RequirePlaceProp | RequireInventoryProp
;

RequirePlaceProp:
    'is_present:' '[' require += [Object] ']'
;

RequireInventoryProp:
    'is_carried:' '[' require += [Object] ']'
;

LocatedProp:
    'at:' located = [Place]
;

CRUDProp:
    type = CRUD ':' item = [Object] 
    (', destination:' place = [Place])? 
;

CRUD:
    'create' | 'delete' | 'move'
;

DescriptionSection:
    '"""'
        'name:' name = /.*/
        'description:' description = /.*/
    '"""'
;

Blockade:
    'blockade {'
        blocks += Block
    '}'
;

Block:
    flag = [Flag] ',' direction = Direction
;

Connections:
    'connections {'
        connections *= Connection
    '}'
;

Connection:
    from_ = [Place] ',' direction = Direction ',' to_ = [Place] 
;

Player:
    'player {'
        'position:' position = [Place]
        'inventory {'
            items *= [Object]
        '}'
    '}'
;

Direction:
    'N' | 'S' | 'E' | 'W'
;

Keyword:
    'place' | 'items' | 'item' | 'name' | 
    'description' | 'pickable' | 'flags' | 'flag' |
    'activated' | 'false_mgs' | 'true_msg' | 'require' |
    'dependencies' | 'blockade' | 'connections' |
    'player' | 'start' | 'inventory'
;

WID: 
    !Keyword ID
;